# 12. Quais são os conceitos ensinados por um determinado mestre (arbitra um)?
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>
SELECT ?conceito
WHERE {
  :Albert_Einstein :ensina ?disciplina .
  ?conceito :éEstudadoEm ?disciplina .
}
ORDER BY ?conceito


# 13. Que obras explicam o conceito de "Relatividade"?
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>

SELECT ?nomeObra
WHERE {
  ?obra :explica :Relatividade .
  ?obra :temNome ?nomeObra .
}
ORDER BY ?nomeObra


# 14. Em que disciplinas se estudam "Ciências Sociais"?
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>

SELECT ?nomeDisciplina
WHERE {
  :Ciências_Sociais :contém ?disciplina .
  ?disciplina :temNome ?nomeDisciplina .
}
ORDER BY ?nomeDisciplina


# 15. Qual é a aplicação prática dos conhecimentos em "Lógica Formal"?
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>

SELECT ?nomeAplicacao
WHERE {
  :Lógica_Formal :temAplicaçãoEm ?aplicacao .
  ?aplicacao :temNome ?nomeAplicacao .
}
ORDER BY ?nomeAplicacao


# 16. Quais disciplinas pertencem ao tipo de conhecimento "Ciências Exatas"?
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>

SELECT ?nomeDisciplina
WHERE {
  ?disciplina :pertenceA :Ciências_Exatas .
  ?disciplina :temNome ?nomeDisciplina .
}
ORDER BY ?nomeDisciplina


# 17. Que mestres ensinaram no século XIX?
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT DISTINCT ?nomeMestre
WHERE {
  {
    ?mestre :temDescrição "Século XIX"^^xsd:string .
    ?mestre :temNome ?nomeMestre .
  }
  UNION
  {
    ?mestre :temDescrição "Século XIX/XX"^^xsd:string .
    ?mestre :temNome ?nomeMestre .
  }
}
ORDER BY ?nomeMestre


# 18. Que aprendizes estudam "Programação"?
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>

SELECT ?nomeAprendiz
WHERE {
  ?aprendiz a :Aprendiz .
  ?aprendiz :aprende ?disciplina .
  :Programação :éEstudadoEm ?disciplina .
  ?aprendiz :temNome ?nomeAprendiz .
}
ORDER BY ?nomeAprendiz


# 19. Qual a distribuição de aprendizes por disciplina? (Resultado será uma lista de pares: Disciplina, número de alunos)
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>

SELECT ?nomeDisciplina (COUNT(?aprendiz) AS ?numeroAprendizes)
WHERE {
  ?aprendiz a :Aprendiz .
  ?aprendiz :aprende ?disciplina .
  ?disciplina :temNome ?nomeDisciplina .
}
GROUP BY ?nomeDisciplina
ORDER BY DESC(?numeroAprendizes)


# 20. Qual a distribuição de conceitos por período histórico? (Resultado será uma lista de pares: Período, número de conceitos)
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>

SELECT ?nomePeriodo (COUNT(?conceito) AS ?numeroConceitos)
WHERE {
  ?periodo a :PeríodoHistorico .
  ?periodo :éOrigemDe ?conceito .
  ?periodo :temNome ?nomePeriodo .
}
GROUP BY ?nomePeriodo
ORDER BY DESC(?numeroConceitos)


# 21. Quais os títulos das obras associadas a cada conceito? (Resultado será uma lista de pares: Conceito, Lista de títulos de obras)
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>

SELECT ?nomeConceito (GROUP_CONCAT(?nomeObra; separator=", ") AS ?listaObras)
WHERE {
  ?conceito a :Conceito .
  ?conceito :temNome ?nomeConceito .
  ?conceito :temObraExplicativa ?obra .
  ?obra :temNome ?nomeObra .
}
GROUP BY ?nomeConceito
ORDER BY ?nomeConceito


# 22. Quais os aprendizes que frequentam cada uma das disciplinas? (Resultado será uma lista de pares: Disciplina, Lista dos nomes dos aprendizes que frequentam a disciplina)
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>

SELECT ?nomeDisciplina (GROUP_CONCAT(?nomeAprendiz; separator=", ") AS ?listaAprendizes)
WHERE {
  ?disciplina a :Disciplina .
  ?disciplina :temNome ?nomeDisciplina .
  ?aprendiz :aprende ?disciplina .
  ?aprendiz :temNome ?nomeAprendiz .
}
GROUP BY ?nomeDisciplina
ORDER BY ?nomeDisciplina


# 23. Qual a distribuição de aprendizes por escalões etários? (Resultado será uma lista de pares: Escalão etário, número aprendizes nesse escalão)
# Considera os seguintes escalões: [17, 34], [35, 44], [45, 54], [55, 64], [65, 74], [75, 100]
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?escalaoEtario (COUNT(?aprendiz) AS ?numeroAprendizes)
WHERE {
  ?aprendiz a :Aprendiz .
  OPTIONAL { ?aprendiz :temIdade ?idade }
  BIND(
    IF(?idade >= 17 && ?idade <= 34, "[17, 34]",
      IF(?idade >= 35 && ?idade <= 44, "[35, 44]", 
        IF(?idade >= 45 && ?idade <= 54, "[45, 54]",
          IF(?idade >= 55 && ?idade <= 64, "[55, 64]",
            IF(?idade >= 65 && ?idade <= 74, "[65, 74]",
              IF(?idade >= 75 && ?idade <= 100, "[75, 100]", "Idade desconhecida")
            )
          )
        )
      )
    ) AS ?escalaoEtario
  )
}
GROUP BY ?escalaoEtario
ORDER BY ?escalaoEtario


# 24. Criar uma nova relação na ontologia, estudaCom, que relaciona um aluno com um mestre
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

INSERT DATA {
  :estudaCom a owl:ObjectProperty ;
    rdfs:domain :Aprendiz ;
    rdfs:range :Mestre .
}


# 25a. Construir/Inserir os triplos da nova relação estudaCom na ontologia
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>

CONSTRUCT {
  ?aprendiz :estudaCom ?mestre .
}
WHERE {
  ?aprendiz a :Aprendiz .
  ?mestre a :Mestre .
  ?aprendiz :aprende ?disciplina .
  ?mestre :ensina ?disciplina .
}

# 25b. Construir/Inserir os triplos da nova relação estudaCom na ontologia
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>

INSERT {
  ?aprendiz :estudaCom ?mestre .
}
WHERE {
  ?aprendiz a :Aprendiz .
  ?mestre a :Mestre .
  ?aprendiz :aprende ?disciplina .
  ?mestre :ensina ?disciplina .
}


# 26. Criar uma nova relação na ontologia, dáBasesPara, que relaciona uma disciplina com uma aplicação
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

INSERT DATA {
  :dáBasesPara a owl:ObjectProperty ;
    rdfs:domain :Disciplina ;
    rdfs:range :Aplicação .
}


# 27a. Construir os triplos da nova relação dáBasesPara na ontologia
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>

CONSTRUCT {
  ?disciplina :dáBasesPara ?aplicacao .
}
WHERE {
  ?disciplina a :Disciplina .
  ?aplicacao a :Aplicação .
  ?conceito :éEstudadoEm ?disciplina .
  ?conceito :temAplicaçãoEm ?aplicacao .
}

# 27b. Inserir os triplos da nova relação dáBasesPara na ontologia
PREFIX : <http://www.semanticweb.org/tomas/ontologies/2025/4/sapienta#>

INSERT {
  ?disciplina :dáBasesPara ?aplicacao .
}
WHERE {
  ?disciplina a :Disciplina .
  ?aplicacao a :Aplicação .
  ?conceito :éEstudadoEm ?disciplina .
  ?conceito :temAplicaçãoEm ?aplicacao .
}

